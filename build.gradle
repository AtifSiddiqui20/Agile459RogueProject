plugins {
    id 'java'
    id 'application'
    id 'jacoco'
}

jacoco {
    toolVersion = "0.8.12"
}

group = 'org.example'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url = "https://jitpack.io"
    }
    maven {
        url = "https://repo.maven.apache.org/maven2"
    }
}

dependencies {
    // AsciiPanel for rendering
    implementation('com.github.trystan:AsciiPanel:master-SNAPSHOT')

    // JUnit for testing
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    testImplementation 'org.mockito:mockito-core:5.7.0'
    testImplementation 'org.jacoco:org.jacoco.agent:0.8.12'
}

application {
    mainClass = 'Rogue'
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "failed", "skipped"
    }
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }
        }
    }
}
check.dependsOn jacocoTestCoverageVerification